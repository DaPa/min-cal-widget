apply plugin: 'com.android.application'
apply plugin: 'witness'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

android {

    compileSdkVersion 26
    buildToolsVersion "25.0.3"

    defaultConfig {

        applicationId "cat.mvmike.minimalcalendarwidget"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 7
        versionName "0.3.0"

        jackOptions {
            enabled true
        }
    }

    /*
     * To sign release builds, create the file gradle.properties in
     * ~/.gradle/ with this content:
     *
     * signingStoreFile=key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreFile') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {

        println "Found sign properties in gradle.properties! Signing buildâ€¦"
        signingConfigs {
            release {
                storeFile file(signingStoreFile)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError true
    }

    compileOptions {
        compileOptions.encoding = "UTF-8"

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// https://developer.android.com/topic/libraries/support-library/packages.html
dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:26.0.0-beta2'
}

// Only do the libraries imported from maven repositories.
// generate using: `gradle -q calculateChecksums | sort -V`
dependencyVerification {
    verify = [
            'com.android.support:animated-vector-drawable:4ac3f595f80819b46f79f20551567b9e07de002852ff91da33de1288626baf1b',
            'com.android.support:appcompat-v7:4b7d00befe406311ef48fbf468668d69fbf8380c7f6810b288e7a532a0ac4e8c',
            'com.android.support:support-annotations:a45345c565a417968afec0f9d8e5cc94773f44fd7a5420e417479d155d89f89b',
            'com.android.support:support-compat:1b560c85f53f2d69230493e90d9eef575bf9881ffd9f495dec57466ced71aecd',
            'com.android.support:support-core-ui:acfccb7f007e19037fed14816c6a9bb65548dcc70ec5defd3d16640497f68acf',
            'com.android.support:support-core-utils:71726ecddfd07b40266445a3d0ba95424dc86fd84e690400f2b2e602d9300b36',
            'com.android.support:support-fragment:02d6f8dc7e05f60919cd02277dc34e6a7081f30442a7e23b5a892ab07c2ba97a',
            'com.android.support:support-media-compat:619c3b121b5bfe31ef1b8fb35641b229c8a9cb3a3f15d10acb5e61d6acc51028',
            'com.android.support:support-v4:23e7194cfc7ee8da5be65560a2382417981acffa541a933e41649f1ca3f2aa0a',
            'com.android.support:support-vector-drawable:8db8b286302e71e739fb468d4e5901d673268516254352d35f43d560809f1e00',
    ]
}

pmd {
    toolVersion = '5.5.2'
    consoleOutput = true
}

task pmdMain(type: Pmd, dependsOn: "assembleDebug") {
    ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml")
    ruleSets = [] // otherwise defaults clash with the list in rules.xml
    source 'src/main/java'
    include '**/*.java'
}

task pmd(dependsOn: [pmdMain]) {}

checkstyle {
    toolVersion = '7.4'
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src/main/java'
    include '**/*.java'

    classpath = files()
}
